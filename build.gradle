plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.unbroken-dome.test-sets' version '4.0.0'
	id "org.flywaydb.flyway" version '7.11.3'
	id 'jacoco'
	id 'pmd'
}

jar {
	enabled = true
	archiveBaseName = 'repartir'
	archiveVersion = '1.0'
}

group = 'ar.com.grupoesfera'
version = '1.0'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

testSets {
	fastAcceptanceTest
	integrationTest
	uiTest
	fastAcceptanceTest
	acceptanceTest
	manualTest
}

dependencies {
	runtimeOnly 'com.h2database:h2'
	
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.2'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:3.0.6'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.4'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	fastAcceptanceTestImplementation 'io.cucumber:cucumber-java:6.10.4'
	fastAcceptanceTestImplementation 'io.cucumber:cucumber-junit-platform-engine:6.10.4'
	fastAcceptanceTestImplementation 'io.cucumber:cucumber-spring:6.10.4'
	fastAcceptanceTestImplementation 'org.flywaydb:flyway-core:7.11.3'

	integrationTestImplementation 'org.flywaydb:flyway-core:7.11.3'
	integrationTestImplementation 'io.rest-assured:rest-assured:4.4.0'
	integrationTestImplementation 'net.javacrumbs.json-unit:json-unit:2.27.0'
	integrationTestImplementation "org.testcontainers:junit-jupiter:1.20.2"
	integrationTestRuntimeOnly 'org.testcontainers:mariadb:1.15.2'

	uiTestImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
	uiTestImplementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
	uiTestImplementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
	uiTestImplementation sourceSets.integrationTest.output

	acceptanceTestImplementation 'io.cucumber:cucumber-java:6.10.4'
	acceptanceTestImplementation 'io.cucumber:cucumber-junit-platform-engine:6.10.4'
	acceptanceTestImplementation 'io.cucumber:cucumber-spring:6.10.4'
	acceptanceTestImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
	acceptanceTestImplementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
	acceptanceTestImplementation 'org.flywaydb:flyway-core:7.11.3'
	acceptanceTestImplementation 'org.testcontainers:junit-jupiter:1.20.2'
	acceptanceTestImplementation'org.apache.httpcomponents.client5:httpclient5:5.2.1'
	acceptanceTestRuntimeOnly 'org.testcontainers:mariadb:1.15.2'

	manualTestImplementation 'org.flywaydb:flyway-core:7.11.3'
	manualTestImplementation 'org.testcontainers:junit-jupiter:1.20.2'
	manualTestRuntimeOnly 'org.testcontainers:mariadb:1.20.2'
}

test {
	useJUnitPlatform()
}

fastAcceptanceTest {
	useJUnitPlatform()
	mustRunAfter test
	systemProperty "cucumber.filter.name", System.getProperty("cucumber.filter.name")
}

integrationTest {
	useJUnitPlatform()
	mustRunAfter fastAcceptanceTest
}

uiTest {
	useJUnitPlatform()
	mustRunAfter integrationTest
	systemProperties = System.properties
}

acceptanceTest {
	useJUnitPlatform()
	mustRunAfter uiTest
	systemProperties = System.properties
	systemProperty "cucumber.filter.name", System.getProperty("cucumber.filter.name")
}

check {
	dependsOn fastAcceptanceTest
	dependsOn integrationTest
	dependsOn uiTest
	dependsOn acceptanceTest
}

flyway {
	user = 'repartirUser'
	password = 'repartirPassword'
	url = 'jdbc:mariadb://localhost:3306/repartir'
	locations = [ 'filesystem:src/main/db/migrations' ]
}

jacoco {
	toolVersion = "0.8.8"
}

jacocoTestReport {
	sourceSets sourceSets.main
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	reports {
		html.destination file("${buildDir}/reports/jacoco/html")
	}
}

pmd {
	toolVersion = "6.49.0"
	ignoreFailures = true
	ruleSets = [
		'rulesets/java/quickstart.xml',
	]
}

task demo(type: org.springframework.boot.gradle.tasks.run.BootRun) {
	systemProperty "spring.profiles.active", "manualTest"
	classpath = sourceSets.manualTest.runtimeClasspath
	main = "ar.com.grupoesfera.repartir.RepartirApplication"
}

// https://stackoverflow.com/a/36130467
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
               TestLogEvent.PASSED,
               TestLogEvent.SKIPPED,
               TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                   TestLogEvent.FAILED,
                   TestLogEvent.PASSED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}